buildscript {

    ext {
        springBootVersion = "1.3.0.BUILD-SNAPSHOT"
        springLoadedVersion = "1.2.0.RELEASE"
        queryDSLVersion = "3.6.3"
        hsqldbVersion = "2.3.2"
        morphiaVersion = "0.105"
        gensonVersion = "0.99"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: "java"
apply plugin: "spring-boot"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }	
}

sourceSets {
    generated {
        java {
            srcDirs = ["src/main/generated"]
        }
    }
}

configurations {
    querydslapt
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "com.mysema.querydsl:querydsl-mongodb:${queryDSLVersion}"
    compile "org.mongodb.morphia:morphia:${morphiaVersion}"
    compile "com.owlike:genson:${gensonVersion}"
    querydslapt "com.mysema.querydsl:querydsl-apt:${queryDSLVersion}"
    runtime "org.hsqldb:hsqldb:${hsqldbVersion}"
}

task generateQueryDSL(type: JavaCompile, group: "build", description: "Gera o codigo das classes do QueryDSL") {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.morphia.MorphiaAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

